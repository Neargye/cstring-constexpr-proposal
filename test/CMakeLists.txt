include(CheckCXXCompilerFlag)

if(CMAKE_CXX_COMPILER_ID MATCHES "MSVC")
    set(OPTIONS /W4)
    check_cxx_compiler_flag(/permissive HAS_PERMISSIVE_FLAG)
    if(HAS_PERMISSIVE_FLAG)
        set(OPTIONS ${OPTIONS} /permissive-)
    endif()

    check_cxx_compiler_flag(/std:c++latest HAS_CPPLATEST_FLAG)
    check_cxx_compiler_flag(/std:c++20 HAS_CPP20_FLAG)
elseif(CMAKE_CXX_COMPILER_ID MATCHES "GNU|Clang")
    set(CMAKE_VERBOSE_MAKEFILE ON)
    set(OPTIONS -Wall -Wextra -pedantic-errors)

    check_cxx_compiler_flag(-std=c++2a HAS_CPP2A_FLAG)
    check_cxx_compiler_flag(-std=c++20 HAS_CPP20_FLAG)
endif()

function(make_test target std sources)
    add_executable(${target} ${sources})
    target_compile_options(${target} PRIVATE ${OPTIONS})
    target_include_directories(${target} PRIVATE 3rdparty/Catch2)
    target_link_libraries(${target} PRIVATE ${CMAKE_PROJECT_NAME})
    set_target_properties(${target} PROPERTIES CXX_EXTENSIONS OFF)
    if(std)
        if(CMAKE_CXX_COMPILER_ID MATCHES "MSVC")
            target_compile_options(${target} PRIVATE /std:${std})
        else()
            target_compile_options(${target} PRIVATE -std=${std})
        endif()
    endif()
    add_test(NAME ${target} COMMAND ${target})
endfunction()

if(HAS_CPP20_FLAG)
    make_test(${CMAKE_PROJECT_NAME}_char-cpp20.t c++20 test_char.cpp)
    make_test(${CMAKE_PROJECT_NAME}_wchar-cpp20.t c++20 test_wchar.cpp)
endif()

if(HAS_CPP2A_FLAG)
    make_test(${CMAKE_PROJECT_NAME}_char-cpp2a.t c++2a test_char.cpp)
    make_test(${CMAKE_PROJECT_NAME}_wchar-cpp2a.t c++2a test_char.cpp)
endif()

if(HAS_CPPLATEST_FLAG)
    make_test(${CMAKE_PROJECT_NAME}_char-cpplatest.t c++latest test_char.cpp)
    make_test(${CMAKE_PROJECT_NAME}_wchar-cpplatest.t c++latest test_wchar.cpp)
endif()
